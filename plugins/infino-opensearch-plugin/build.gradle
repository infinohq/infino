import org.opensearch.gradle.test.RestIntegTestTask

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.yaml-rest-test'
apply plugin: 'opensearch.pluginzip'

def pluginName = 'infino-opensearch-plugin'
def pluginDescription = 'A plugin that enables the Infino Telemetry DB to be used with OpenSearch'
def projectPath = 'org.opensearch'
def pathToPlugin = 'infino'
def pluginClassName = 'InfinoPlugin'
group = "InfinoGroup"

apply plugin: 'java'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// We don't have to use OpenSearch license for this plugin
licenseHeaders.enabled = false
dependencyLicenses.enabled = false

// jarhell 
jarHell.enabled = false

// OpenSearch don't support running gradle with a JVM that is in FIPS 140 mode, so skip tests.
// WaitForHttpResourceTests tests would fail as they use JKS/PKCS12 keystores
if (org.opensearch.gradle.info.BuildParams.inFipsJvm) {
    test.enabled = false
    testingConventions.enabled = false
}

// OpenSearch forbids @Test annotations by default, so we disable that check for unit tests.
tasks.named('forbiddenApisMain').configure { onlyIf { false } }
tasks.named('forbiddenApisTest').configure { onlyIf { false } }

task preparePluginPathDirs {
    mustRunAfter clean
    doLast {
        def newPath = pathToPlugin.replace(".", "/")
        mkdir "src/main/java/org/opensearch/$newPath"
        mkdir "src/test/java/org/opensearch/$newPath"
        mkdir "src/yamlRestTest/java/org/opensearch/$newPath"
    }
}

publishing {
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
              name = pluginName
              description = pluginDescription
              groupId = "org.opensearch.plugin"
              licenses {
                license {
                  name = "The Apache License, Version 2.0"
                  url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
              }
              developers {
                developer {
                  name = "Infino"
                  url = "https://github.com/infinohq/infino"
                }
              }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

// No need to validate pom, as we do not upload to maven/sonatype
validateNebulaPom.enabled = false

// Buildscript needs a redundant declaration of opensearch_version. Not sure why.
buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "2.11.1")
        opensearch_group = "org.opensearch"

        // breakdown something like "3.0.0-rc1-snapshot" to the aray
        // ["3.0.0", "rc1", "SNAPSHOT"]. We'll need this to craft
        // the plugin version in the allprojects block
        opensearch_version_tokens = opensearch_version.tokenize('-')
    }

    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

ext {
    // Function to pad version numbers for comparison
    def padVersion = { v -> v.tokenize('.').collect { String.format('%04d', it.toInteger()) }.join('.') }

    // padding the OpenSearch version
    // grab just the version number (opensearch_version_tokens[0]) and ignore the
    // the qualifiers like SNAPSHOT or beta etc.
    currentVersionPadded = padVersion(opensearch_version_tokens[0])
    version210Padded = padVersion('2.10.0')
    version211Padded = padVersion('2.11.0')
    version29Padded = padVersion('2.9.0')
    version27Padded = padVersion('2.7.0')
    version26Padded = padVersion('2.6.0')
}

allprojects {
    group = "org.opensearch"
    // from the version tokens generated earlier craft the plugin version.
    // OpenSearch convention recommends that if the OpenSearch version to
    // plugin version mapping is as follows
    //  3.0.0 -> 3.0.0.0
    //  3.0.0-SNAPSHOT -> 3.0.0.0-SNAPSHOT
    // Use the opensearch_version_tokens to craft the appropriate plugin versions
    version = opensearch_version_tokens[0] + '.0'
    if (opensearch_version_tokens.size > 1) {
        version += '-' + opensearch_version_tokens.drop(1).join('-')
    }
}

opensearchplugin {
    name pluginName
    description pluginDescription
    classname "${projectPath}.${pathToPlugin}.${pluginClassName}"
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

dependencies {
    def mockitoVersion
    def jnaVersion
    if (currentVersionPadded >= version211Padded) {
        mockitoVersion = '5.5.0'
        jnaVersion = '5.13.0'
    } else if (currentVersionPadded >= version210Padded) {
        mockitoVersion = '5.4.0'
        jnaVersion = '5.5.0'
    } else if (currentVersionPadded >= version29Padded) {
        mockitoVersion = '5.4.0'
        jnaVersion = '5.5.0'
    } else if (currentVersionPadded >= version27Padded) {
        mockitoVersion = '5.2.0'
        jnaVersion = '5.5.0'
    } else if (currentVersionPadded >= version26Padded) {
        mockitoVersion = '5.1.0'
        jnaVersion = '5.5.0'
    } else {
        mockitoVersion = '4.7.0'
        jnaVersion = '5.5.0'
    }

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    implementation "net.java.dev.jna:jna:$jnaVersion"
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'com.google.code.gson:gson:2.7'

}

repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

test {
    include '**/*Tests.class'
    environment "INFINO_SERVER_URL", "http://test-host:3000"

    // Turn off the security manager for unit tests.
    jvmArgs '-Dtests.security.manager=false'
}

task infinoDockerComposeUp(type: Exec) {
    commandLine 'docker-compose', '-f', 'src/main/resources/docker-compose.yml', 'up', '-d'
    // Assuming you want to capture logs after bringing up the services
    doLast {
        exec {
            commandLine 'docker-compose', '-f', 'src/main/resources/docker-compose.yml', 'logs'
            standardOutput = new FileOutputStream(new File(project.buildDir, 'compose.out'))
            errorOutput = new FileOutputStream(new File(project.buildDir, 'compose.err'))
        }
    }
}

task infinoDockerComposeDown(type: Exec) {
    commandLine 'docker-compose', '-f', 'src/main/resources/docker-compose.yml', 'down'
    doLast {
        println 'Infino Docker Compose Down executed'
    }
}

task integTest(type: RestIntegTestTask) {
    dependsOn infinoDockerComposeUp
    finalizedBy infinoDockerComposeDown
    description = "Run tests against a cluster"
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}
tasks.named("check").configure { dependsOn(integTest) }

integTest {
    // The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
    if (System.getProperty("test.debug") != null) {
        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
    // Turn off the security manager for integration tests.
    jvmArgs '-Dtests.security.manager=false'
}

testClusters.integTest {
    testDistribution = "INTEG_TEST"

    // This installs our plugin into the testClusters
    plugin(project.tasks.bundlePlugin.archiveFile)
}

run {
    useCluster testClusters.integTest
}

// updateVersion: Task to auto update version to the next development iteration
task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
         // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}

task fetchDockerLogs {
    doLast {
        exec {
            commandLine 'docker-compose', '-f', 'src/main/resources/docker-compose.yml', 'logs'
            standardOutput = new FileOutputStream(new File(project.buildDir, 'infino-docker-compose-logs.out'))
        }
    }
}

// tasks.named('yamlRestTest') {
//     dependsOn infinoDockerComposeUp
//     mustRunAfter infinoDockerComposeUp
//     finalizedBy fetchDockerLogs
//     finalizedBy infinoDockerComposeDown

// }

// tasks.named("check").configure { dependsOn('yamlRestTest') }
